```scheme
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))

(define (improve guess x)
  (average guess (/ x guess)))

(define (average x y)
  (/ (+ x y) 2))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (abs x)
  (if (< x 0)
      (- x)
      x))

(define (square x)
  (* x x))

(define (sqrt x)
  (sqrt-iter 1.0 x))

1 ]=> (sqrt 9)
; Value: 3.00009155413138
```

Alyssa将if重写了一下：

```scheme
(define (new-if predicate then-clause else-clause)
  (cond (predicate then-clause)
    	(else else-clause)))
```

将sqrt-iter改成：

```scheme
(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x)
                     x)))
1 ]=> (sqrt 9)
;Aborting!: maximum recursion depth exceeded
```

（在网上查询了一下才知道原因）在执行的时候解释器说递归层数超过了最大深度，问题自然是出现在新定义的`new-if`上了。

书本12页所说，`if`是一种特殊的语句形式，当\<predicate\>为真，就去求值\<consequent\>（即这里的`then-clause`），否则就去求\<alternative\>（即这里的`else-clause`）。

另一方面，新定义的`new-if`只是一个普通函数，它没有if的特殊形式，根据解释器所使用的应用序求值，函数每个部分都会求值，因此，当使用`new-if`时，无论\<predicate\>的真假，`then-clause`和`else-clause`都会求值。

```scheme
1 ]=> (if #t (display "one") (display "two"))
one
;Unspecified return value

1 ]=> (new-if #t (display "one") (display "two"))
twoone
;Unspecified return value

```

这就说明了为什么`sqrt-iter`会出错：无论如何，`sqrt-iter`都会一直递归下去。
